# Input a debug key to RP2350 to enable access when keys 5 or 6 are
# configured in OTP.

transport select swd

source [find target/swj-dp.tcl]

if { [info exists CHIPNAME] } {
        set _CHIPNAME $CHIPNAME
} else {
        set _CHIPNAME rp2350
}

if { [info exists WORKAREASIZE] } {
        set _WORKAREASIZE $WORKAREASIZE
} else {
        set _WORKAREASIZE 0x10000
}

if { [info exists CPUTAPID] } {
        set _CPUTAPID $CPUTAPID
} else {
        set _CPUTAPID 0x00040927
}

if { [info exists DEBUGKEY] } {
    set _DEBUGKEY $DEBUGKEY
} else {
    echo "RP2350: Using example debug key, set your own with '-c set DEBUGKEY {0x12 0x34 ..}'"
    set _DEBUGKEY {0xad 0xde 0xef 0xbe 0x23 0x01 0x67 0x45 0xab 0x89 0xef 0xcd 0xc3 0xc3 0xa5 0xa5}
}

swj_newdap $_CHIPNAME swd -expected-id $_CPUTAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.swd -adiv6

init

# Write RP_AP.DBGKEY RESET bit
rp2350.dap apreg 0x80000 0x4 0x4

# Write the key bytes bit-by-bit to RP_AP.DBGKEY
proc rp2350_dbgkey_send_bits {byte} {
    for {set i 0} {$i < 8} {incr i} {
        set mask [expr {1 << $i}]
        if {[expr {$byte & $mask}]} {
            rp2350.dap apreg 0x80000 0x4 0x3
        } else {
            rp2350.dap apreg 0x80000 0x4 0x2
        }
    }
}

foreach _byte $_DEBUGKEY {
    rp2350_dbgkey_send_bits $_byte
}

proc rp2350_dbgkey_print_status {corename cswval} {
    # Check CSW SDeviceEn bit
    if {[expr {$cswval & 0x800000}]} {
        set secdebug "allowed"
    } else {
        set secdebug "denied"
    }

    # Check CSW DeviceEn bit
    if {[expr {$cswval & 0x40}]} {
        set nsecdebug "allowed"
    } else {
        set nsecdebug "denied"
    }

    puts "Core $corename CSW value: $cswval (secure debug: $secdebug, non-secure debug: $nsecdebug)"
}

set _CORE0_CSW [rp2350.dap apreg 0x2000 0xd00]
set _CORE1_CSW [rp2350.dap apreg 0x4000 0xd00]

rp2350_dbgkey_print_status "0" $_CORE0_CSW
rp2350_dbgkey_print_status "1" $_CORE1_CSW

echo "Now attach a debugger to your RP2350 and load some code"
exit
